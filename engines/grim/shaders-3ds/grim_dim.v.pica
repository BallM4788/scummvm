; dimming shader for Grim Fandango in SCUMMVM

; ORIGINAL GLSL CODE
;	in vec2 position;
;	in vec2 texcoord;
;
;	uniform vec2 scaleWH;
;
;	out vec2 Texcoord;
;
;	void main() {
;		Texcoord = texcoord;
;		// Coordinates are [0.0;1.0], transform to [-1.0; 1.0]
;		vec2 pos = position * scaleWH;
;		pos.x = pos.x * 2.0 - 1.0;
;		pos.y = pos.y * 2.0 - 1.0;
;
;		gl_Position = vec4(pos, 0.0, 1.0);
;	}

; NOTES
;	- The 3DS does not support custom fragment shaders, so we
;		do the dimming beforehand, as is done in "gfx_opengl.cpp"
;		when not using ARB shaders.


; Constants
.constf myConst(0.0, 1.0, 2.0, 0.5)
.alias ONES			 myConst.yyyy
.alias TWOS			 myConst.zzzz
.alias HALF			 myConst.wwww
.alias NEGONES		-myConst.yyyy


; Inputs
.alias    inPos		v0		; in vec2 position
.alias inTcoord		v1		; in vec2 texcoord


; Uniforms
.fvec scaleWH				; vec2 scaleWH


; Outputs
.out outPos			pos		; gl_Position
.out outTcoord		tcoord0	; out vec2 Texcoord


; Main
.entry main
.proc main
	mov outTcoord.xy,	 inTcoord.xy
	mul r0.xy,			 scaleWH.xy,		inPos.xy
	mov r1.xy,			 TWOS
	mad outPos.xy,		 r0.xy,				r1.xy,				NEGONES
	; 3DS system clips z component of window coordinates in the range [0, -1] instead of [-1, 1]
	; z-component 0.0 in openGL -> -0.5 in 3DS
	mov outPos.z,		-HALF
	mov outPos.w,		 ONES

	end
.end
